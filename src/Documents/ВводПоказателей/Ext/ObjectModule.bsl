Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ЗначенияПоказателей
	Движения.ЗначенияПоказателей.Записывать = Истина;
	Для Каждого СтрокаТабЧасти Из Показатели Цикл
		Движение = Движения.ЗначенияПоказателей.Добавить();
		Движение.Период = Дата;
		Движение.Периодичность = Периодичность;
		Движение.Контрагент = Контрагент;
		Движение.СтандартОтчетности = СтандартОтчетности;
		Движение.Показатель = СтрокаТабЧасти.Показатель;
		Движение.ЗначениеПоказателя = СтрокаТабЧасти.ЗначениеПоказателя * Множитель;
	КонецЦикла;
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	ДатаНачПериода = ОбщегоНазначенияКлиентСервер.ПолучитьДатуНачалаПериода(Дата, Периодичность);
	Если Дата <> ДатаНачПериода Тогда
		Дата = ДатаНачПериода;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Множитель) Тогда
		Множитель = 1;
	КонецЕсли;
	
	// АВТОПРОВЕРКА
	ЗначенияПоказателей = Новый Соответствие;
	Для Каждого СтрокаТабЧасти Из ЭтотОбъект.Показатели Цикл
		ЗначенияПоказателей.Вставить(СтрокаТабЧасти.Показатель, СтрокаТабЧасти.ЗначениеПоказателя);
	КонецЦикла; 
	Для Каждого СтрокаТабЧасти Из ЭтотОбъект.Показатели Цикл
		РезультатПроверки = ОбщегоНазначенияСервер.ПроверитьЗначениеПоказателя(ЗначенияПоказателей, СтрокаТабЧасти.Показатель);
		Если РезультатПроверки <> Неопределено
			И (НЕ РезультатПроверки) Тогда
			СообщениеОбОшибке = "Для показателя " + СтрокаТабЧасти.Показатель + " не выполняется условие проверки """ + СтрокаТабЧасти.Показатель.ФормулаПроверки + "!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,, Отказ);
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Контрагент");
	ПроверяемыеРеквизиты.Добавить("Периодичность");
	
КонецПроцедуры
