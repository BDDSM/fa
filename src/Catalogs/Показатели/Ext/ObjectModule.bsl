
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	БуквыРус	= "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ";
	БуквыЛат	= "ABCDEFGHIJKLMONPQRSTUVWXYZ";
	Цифры		= "0123456789";
	Для Сч = 1 По СтрДлина(УсловноеОбозначение) Цикл
		ТекСимв = ВРег(Сред(УсловноеОбозначение, Сч, 1));
		Если Найти(БуквыРус, ТекСимв) = 0
			И Найти(БуквыЛат, ТекСимв) = 0
			И Найти(Цифры, ТекСимв) = 0 Тогда
			СообщениеОбОшибке = "Условное обозначение может содержать только буквы и цифры! Ошибка в символе № " + Сч + "!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ЭтотОбъект, "УсловноеОбозначение",, Отказ);
		КонецЕсли; 
	КонецЦикла; 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Показатели.Наименование КАК Наименование,
	|	Показатели.Код КАК Код
	|ИЗ
	|	Справочник.Показатели КАК Показатели
	|ГДЕ
	|	Показатели.УсловноеОбозначение = &УсловноеОбозначение";
	Запрос.УстановитьПараметр("УсловноеОбозначение", УсловноеОбозначение);
	Если НЕ ЭтоНовый() Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И Показатели.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	КонецЕсли; 
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СообщениеОбОшибке = "В базе уже есть показатель с Условным обозначением """ + УсловноеОбозначение + """: " 
						+ """" + Выборка.Наименование + """" + " (код " + Выборка.Код + ")!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ЭтотОбъект, "УсловноеОбозначение",, Отказ);
	КонецЕсли; 
	
	Если Расчетный И ПустаяСтрока(ФормулаРасчета) Тогда
		СообщениеОбОшибке = "У показателя с признаком Расчетный должна быть заполнена Формула расчета!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ЭтотОбъект, "ФормулаРасчета",, Отказ);
	КонецЕсли; 
	
	Если СпособРасчетаИерархии = Перечисления.СпособыРасчетаИерархии.ПоФормуле
		И ПустаяСтрока(ФормулаРасчетаИерархииСКД) Тогда
		СообщениеОбОшибке = "У показателя с со способом расчета иерархии = По формуле Формула расчета иерархии СКД!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ЭтотОбъект, "ФормулаРасчетаИерархииСКД",, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("УсловноеОбозначение");
	
КонецПроцедуры
