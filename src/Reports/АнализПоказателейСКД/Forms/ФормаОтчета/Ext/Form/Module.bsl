	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	// настройка СКД
	СхемаКомпоновкиДанных = ОтчетОбъект.ПолучитьСхемуКомпоновкиДанных();
	ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");
	
	Отчет.АдресСКД = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Отчет.АдресСКД));
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// значения по умолчанию
	Отчет.Дата2 = КонецГода(ТекущаяДата());
	Отчет.Дата1 = НачалоГода(ДобавитьМесяц(Отчет.Дата2, -3*12));
	Отчет.Периодичность = Перечисления.Периодичность.Год;
	Отчет.СтандартОтчетности = Справочники.СтандартыОтчетности.МСФО;
	
	Отчет.ФлагИспользоватьВыделениеЦветом = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)

	Отчет.Дата1 = Период.ДатаНачала;
	Отчет.Дата2 = Период.ДатаОкончания;
	
КонецПроцедуры

&НаСервере
Функция СохранитьСКДНаСервере()
	
	СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьСхемуКомпоновкиДанных();
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СхемаКомпоновкиДанных);
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции

&НаКлиенте
Процедура ПосмотретьСКД(Команда)
	
	Текст = Новый ТекстовыйДокумент;
	Стр = СохранитьСКДНаСервере();
	Текст.УстановитьТекст(Стр);
	Текст.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Период.ДатаНачала = Отчет.Дата1;
	Период.ДатаОкончания = Отчет.Дата2;
	
КонецПроцедуры
