
Функция РассчитатьЗначениеПоказателя(ЗначенияПоказателей, Показатель, ЗначениеПоУмолчанию = Неопределено, ВыводитьПредупреждения = Истина) Экспорт
	Результат = ЗначениеПоУмолчанию;
	
	Если 1=2 Тогда
		Показатель = Справочники.Показатели.ПустаяСсылка();
	КонецЕсли; 
	
	Формула = Показатель.ФормулаРасчета;
	
	Если ПустаяСтрока(Формула) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат ВычислитьЗначениеПоФормуле(ЗначенияПоказателей, Показатель, Формула, ЗначениеПоУмолчанию, ВыводитьПредупреждения);
	
КонецФункции

Функция ПроверитьЗначениеПоказателя(ЗначенияПоказателей, Показатель, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Результат = ЗначениеПоУмолчанию;
	
	Если 1=2 Тогда
		Показатель = Справочники.Показатели.ПустаяСсылка();
	КонецЕсли; 
	
	Формула = Показатель.ФормулаПроверки;
	
	Если ПустаяСтрока(Формула) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат ВычислитьЗначениеПоФормуле(ЗначенияПоказателей, Показатель, Формула, ЗначениеПоУмолчанию);
	
КонецФункции

Функция ПолучитьИспользуемыеУсловныеОбозначения(Формула) Экспорт 
	
	ИспользумыеУсловныеОбозначения = Новый Массив; 
	Стр = Формула;
	
	НачПоз = Найти(Стр, "[");
	ТекПоз = НачПоз;
	Пока НачПоз > 0 Цикл
		
		НачалоСообщенияОбОшибке = "Ошибка в формуле """ + Формула + """. Поз. #" + ТекПоз + ": ";
		
		КонПоз = Найти(Стр, "]");
		Если КонПоз = 0 
			Или КонПоз <= НачПоз Тогда
		    ВызватьИсключение НачалоСообщенияОбОшибке + "Проверьте правильность указания символов ""["" и ""]""!";
		КонецЕсли; 
		
		ТекУслОбозн = Сред(Стр, НачПоз + 1, КонПоз - НачПоз - 1);
		
		Если ПустаяСтрока(ТекУслОбозн) Тогда
		    ВызватьИсключение НачалоСообщенияОбОшибке + "Не задано наименование показателя!";
		КонецЕсли; 
		
		ТекУслОбозн = СтрЗаменить(ТекУслОбозн, """", """""");
		
		Если ИспользумыеУсловныеОбозначения.Найти(ТекУслОбозн) = Неопределено Тогда
			ИспользумыеУсловныеОбозначения.Добавить(ТекУслОбозн);
		КонецЕсли; 
		
		Стр = Сред(Стр, КонПоз + 1);
		ТекПоз = ТекПоз + КонПоз + 1 - НачПоз;
		
		НачПоз = Найти(Стр, "[");
		ТекПоз = ТекПоз + НачПоз - 1;
		
	КонецЦикла; 
	
	Возврат ИспользумыеУсловныеОбозначения;
	
КонецФункции

Функция ПолучитьТаблицуПоказателей() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Показатели.Ссылка КАК Показатель,
	|	Показатели.Родитель КАК Родитель,
	|	Показатели.Наименование КАК Наименование,
	|	Показатели.УсловноеОбозначение КАК УсловноеОбозначение,
	|	Показатели.Порядок КАК Порядок,
	|	Показатели.Расчетный КАК Расчетный,
	|	Показатели.Периодический КАК Периодический,
	|	Показатели.УстановленоЗначениеПоУмолчанию КАК УстановленоЗначениеПоУмолчанию,
	|	Показатели.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию,
	|	Показатели.ФормулаРасчета КАК ФормулаРасчета,
	|	Показатели.ФормулаПроверки КАК ФормулаПроверки,
	|	Показатели.СпособРасчетаИерархии КАК СпособРасчетаИерархии,
	|	Показатели.ФормулаРасчетаИерархииСКД КАК ФормулаРасчетаИерархииСКД,
	|	Показатели.ИспользоватьПоследнееЗначениеДляПоследнегоПериода КАК ИспользоватьПоследнееЗначениеДляПоследнегоПериода
	|ИЗ
	|	Справочник.Показатели КАК Показатели
	|ГДЕ
	|	НЕ Показатели.ЭтоГруппа
	|	И НЕ Показатели.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Показатель
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ТаблицаПоказателей = Запрос.Выполнить().Выгрузить();
	ТаблицаПоказателей.Индексы.Добавить("Показатель");
	ТаблицаПоказателей.Индексы.Добавить("УсловноеОбозначение");
	
	Возврат ТаблицаПоказателей; 
	
КонецФункции

Функция СтруктураУсловногоОбозначения(УсловноеОбозначение) Экспорт
	
	Результат = Новый Структура("УсловноеОбозначение, ИндексПериода", УсловноеОбозначение, 0); 
	
	Стр = УсловноеОбозначение;
	
	НачПоз = Найти(Стр, "(");
	Если НачПоз > 0 Тогда
		// есть индекс периода
		КонПоз = Найти(Стр, ")");
		Если КонПоз = 0 
			Или КонПоз <= НачПоз 
			Или КонПоз <> СтрДлина(Стр) Тогда
		    ВызватьИсключение "Ошибка синтаксиса в показателе """ + УсловноеОбозначение + """: Проверьте правильность указания символов ""("" и "")""!";
		КонецЕсли; 
		Результат.УсловноеОбозначение = Лев(Стр, НачПоз - 1);
		Результат.ИндексПериода = Число(Сред(Стр, НачПоз + 1, КонПоз - НачПоз - 1));
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьУсловныеОбозначенияВсехВлияющихПоказателейПоОдномуПоказателю(Показатель, ТаблицаПоказателей, БезИндексовПериодов = Истина, Знач УжеРазобранныеРанее = Неопределено)
	
	СтрокаТаблицыПоказателей = Неопределено;
	Если ТипЗнч(Показатель) = Тип("СправочникСсылка.Показатели") Тогда
		СтрокаТаблицыПоказателей = ТаблицаПоказателей.Найти(Показатель, "Показатель");
	Иначе 
		УсловноеОбозначениеБезИндексаПериода = СтруктураУсловногоОбозначения(Показатель).УсловноеОбозначение;
		СтрокаТаблицыПоказателей = ТаблицаПоказателей.Найти(УсловноеОбозначениеБезИндексаПериода, "УсловноеОбозначение");
	КонецЕсли;
					
	Если СтрокаТаблицыПоказателей = Неопределено Тогда
		ВызватьИсключение "Показатель """ + Показатель + """ не найден в таблице показателей!";
	КонецЕсли; 
	
	Результат = Новый Массив;
	
	Если НЕ СтрокаТаблицыПоказателей.Расчетный Тогда
		Возврат Результат;
	КонецЕсли; 
	
	Если УжеРазобранныеРанее = Неопределено Тогда
		УжеРазобранныеРанее = Новый Массив;  
	КонецЕсли; 
	
	ИспользумыеУсловныеОбозначения = ПолучитьИспользуемыеУсловныеОбозначения(СтрокаТаблицыПоказателей.ФормулаРасчета);
	
	Для Каждого ТекУслОбозн Из ИспользумыеУсловныеОбозначения Цикл
		
		ТекУслОбознБезИндексаПериода = СтруктураУсловногоОбозначения(ТекУслОбозн).УсловноеОбозначение;
		
		ДобавляемоеУслОбозн = ?(БезИндексовПериодов, ТекУслОбознБезИндексаПериода, ТекУслОбозн);
	
		Если Результат.Найти(ДобавляемоеУслОбозн) = Неопределено Тогда
			Результат.Добавить(ДобавляемоеУслОбозн);
		КонецЕсли;
		
		Если УжеРазобранныеРанее.Найти(ДобавляемоеУслОбозн) = Неопределено Тогда
			МассивПредков = ПолучитьУсловныеОбозначенияВсехВлияющихПоказателейПоОдномуПоказателю(ДобавляемоеУслОбозн, ТаблицаПоказателей, БезИндексовПериодов, Результат);
			Для Каждого ТекПредок Из МассивПредков Цикл
				Если Результат.Найти(ТекПредок) = Неопределено Тогда
					Результат.Добавить(ТекПредок);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		
	КонецЦикла; 
	
	Возврат Результат;
	
	
КонецФункции

Функция ПолучитьУсловныеОбозначенияВсехВлияющихПоказателей(ПоказательИлиМассивПоказателей, ТаблицаПоказателей, БезИндексовПериодов = Истина) Экспорт 
	
	Результат = Новый Массив;
	
	Если ТипЗнч(ПоказательИлиМассивПоказателей) = Тип("Массив") Тогда
		МассивПоказателей = ПоказательИлиМассивПоказателей;
		Для Каждого ТекПоказатель Из МассивПоказателей Цикл
			УсловныеОбозначения = ПолучитьУсловныеОбозначенияВсехВлияющихПоказателейПоОдномуПоказателю(ТекПоказатель, ТаблицаПоказателей, БезИндексовПериодов, Результат);
			Для Каждого ТекУслОбозн Из УсловныеОбозначения Цикл
				Если Результат.Найти(ТекУслОбозн) = Неопределено Тогда
					Результат.Добавить(ТекУслОбозн);
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла; 
	Иначе 
		ТекПоказатель = ПоказательИлиМассивПоказателей;
		Результат = ПолучитьУсловныеОбозначенияВсехВлияющихПоказателейПоОдномуПоказателю(ТекПоказатель, ТаблицаПоказателей, БезИндексовПериодов);
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

Функция ВычислитьЗначениеПоФормуле(ЗначенияПоказателей, Показатель, Формула, ЗначениеПоУмолчанию = Неопределено, ВыводитьПредупреждения = Истина)
	
	Значения = Новый Соответствие;
	
	Для Каждого КлючИЗначение Из ЗначенияПоказателей Цикл
		
		ТекПоказатель = КлючИЗначение.Ключ;
		ТекЗнач = КлючИЗначение.Значение;
		ТекУслОбозн = ?(ТипЗнч(ТекПоказатель) = Тип("СправочникСсылка.Показатели"), 
					    ТекПоказатель.УсловноеОбозначение,
						ТекПоказатель);

		Значения.Вставить(ТекУслОбозн, ТекЗнач);
		
	КонецЦикла; 
	
	НачалоСообщенияОбОшибке = "Ошибка расчета показателя """ + Показатель + """ по формуле """ + Формула + """! ";
	
	Попытка
		ИспользумыеУсловныеОбозначения = ПолучитьИспользуемыеУсловныеОбозначения(Формула);
	Исключение
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НачалоСообщенияОбОшибке + "Описание ошибки = " + ОписаниеОшибки());
		Возврат ЗначениеПоУмолчанию;
	КонецПопытки;
	
	ФормулаДляВычисления = Формула;
	
	Для Каждого ТекУслОбозн Из ИспользумыеУсловныеОбозначения Цикл
		
		Если Значения.Получить(ТекУслОбозн) = Неопределено Тогда
			Если ВыводитьПредупреждения Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НачалоСообщенияОбОшибке + "Значение показателя """ + ТекУслОбозн + """ не установлено!");
			КонецЕсли; 
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли; 
		
		ФормулаДляВычисления = СтрЗаменить(ФормулаДляВычисления, 
			"[" + ТекУслОбозн + "]",
			"Значения.Получить(""" + ТекУслОбозн + """)");
			
	КонецЦикла; 
	
	УстановитьБезопасныйРежим(Истина);
	Попытка
		Результат = Вычислить(ФормулаДляВычисления);
	Исключение
		Результат = ЗначениеПоУмолчанию;
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НачалоСообщенияОбОшибке
			+ "Техн.информация: Формула для вычисления = """ + ФормулаДляВычисления + ". Описание ошибки = " + ОписаниеОшибки());
	КонецПопытки; 
	УстановитьБезопасныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции